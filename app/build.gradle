import com.onemillionworlds.DeepTokenifyDirectory
import com.onemillionworlds.DeepTokenifyFile

plugins {
    id 'com.android.application'
    id "io.github.0ffz.github-packages" version "1.2.1" // Plugin for anonymous inclusion of artifacts hosted in github package registry
}

android {
    compileSdk 35

    defaultConfig {
        applicationId "com.onemillionworlds.TamarinTestBedAndroid"
        minSdk 28
        targetSdk 35
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        viewBinding true
    }
    packaging {
        jniLibs {
            useLegacyPackaging true
        }
        resources {
            excludes += ["com/simsilica/lemur/style/base/glass-styles.groovy"]
        }
    }
    namespace 'com.onemillionworlds.android'
}

ext.jmonkeyengineVersion = '3.8.1-stable'

configurations.all {
    exclude group: 'org.jmonkeyengine', module: 'jme3-desktop'
}


dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.navigation:navigation-fragment:2.3.5'
    implementation 'androidx.navigation:navigation-ui:2.3.5'
    implementation 'org.jmonkeyengine:jme3-android:'+jmonkeyengineVersion
    implementation 'org.jmonkeyengine:jme3-android-native:'+jmonkeyengineVersion
    implementation 'org.jmonkeyengine:jme3-core:'+jmonkeyengineVersion
    implementation 'org.jmonkeyengine:jme3-effects:'+jmonkeyengineVersion
    implementation 'org.jmonkeyengine:jme3-plugins:'+jmonkeyengineVersion
    implementation 'org.jmonkeyengine:jme3-testdata:'+jmonkeyengineVersion
    implementation('com.onemillionworlds:deeptokens:1.2.0') {
        exclude group: 'org.jmonkeyengine', module: 'jme3-core'
        exclude group: 'org.jmonkeyengine', module: 'jme3-lwjgl3'  // removes LWJGL transitives
        exclude group: 'org.lwjgl'                                  // belt-and-braces on LWJGL
    }

    implementation "com.onemillionworlds:tamarin-android:3.0.0-alpha2"


    implementation 'com.simsilica:lemur:1.16.0'
    implementation 'com.simsilica:lemur-proto:1.13.0'

    implementation "com.google.guava:guava:33.5.0-android" // <-- I don't understand why this is necessary as lemur brings it in, but the Selector dropdown won't work with out this
    implementation 'com.github.stephengold:Minie:9.0.1'

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

}


tasks.register("generateButtonPanel", DeepTokenifyFile){
    inputFile = rootProject.file('deepTokenTemplates/buttonPanel.png')
    outputFile = file('src/main/generated/resources/Models/buttonPanel.j3o')
    tokenWidth = 2f
    tokenDepth = 0.05f
}

tasks.register("generateButtons", DeepTokenifyDirectory){
    inputDirectory = rootProject.file('deepTokenTemplates/buttons')
    outputDirectory = file('src/main/generated/resources/Models/buttons')
    tokenWidth = 0.25f
    tokenDepth = 0.1f
}

// Include the generated Java resources directory in the Android source set
android {
    sourceSets {
        main {
            resources.srcDirs += ['src/main/generated/resources']
        }
    }
}

// On Android, there is no processResources task. Hook generation to run before the build.
tasks.named("preBuild") {
    dependsOn("generateButtons", "generateButtonPanel")
}
